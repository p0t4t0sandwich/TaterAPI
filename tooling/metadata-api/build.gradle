plugins {
    id('maven-publish')
}

dependencies {
    compileOnly('org.spongepowered:mixin:0.8.5')
    compileOnly('org.ow2.asm:asm-tree:6.2')
    compileOnly(project(':tooling:entrypoint-spoof'))
}

java.toolchain.languageVersion = JavaLanguageVersion.of(java_version)
sourceCompatibility = targetCompatibility = java_version

jar {
    archiveFileName = "metadata-api-${metadata_api_version}.jar"

    manifest {
        attributes 'Specification-Title': 'Metadata API',
                'Specification-Version': metadata_api_version,
                'Specification-Vendor': 'NeualNexus',
                'Implementation-Version': metadata_api_version,
                'Implementation-Vendor': 'NeualNexus',
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
    }

    from(["README.md"]) {
        into("META-INF")
    }
}

publishing {
    publications {
        shadow(MavenPublication) {
            groupId = group_id
            artifactId = "metadata-api"
            version = "${metadata_api_version}"

            artifact(jar) {
                classifier = ''
            }

            pom {
                name = 'metadata-api'
                description = 'An abstract API for querying modloader metadata at runtime'
                url = 'https://github.com/p0t4t0sandwich/TaterLib/tree/main/tooling/modloader-meta-api/README.md'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://github.com/p0t4t0sandwich/TaterLib/blob/main/LICENSE-API'
                    }
                }
                developers {
                    developer {
                        id = 'p0t4t0sandwich'
                        name = 'Dylan Sperrer'
                        email = 'dylan@neuralnexus.dev'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/p0t4t0sandwich/TaterLib.git'
                    url = 'https://github.com/p0t4t0sandwich/TaterLib'
                }
            }
        }
    }

    repositories {
        mavenLocal()
        maven {
            name = "NeuralNexusReleases"
            url = "https://maven.neuralnexus.dev/releases"
            credentials {
                username = project.findProperty("neuralNexusUsername") ?: System.getenv("NEURALNEXUS_USERNAME")
                password = project.findProperty("neuralNexusPassword") ?: System.getenv("NEURALNEXUS_PASSWORD")
            }
        }
    }
}

artifacts {
    archives(jar)
}
